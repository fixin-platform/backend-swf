#!/usr/bin/env coffee

path = require "path"
parse = require "path-parse"
yargs = require "yargs"
domain = require "domain"
Registrar = require "../lib/Actor/Registrar"
createLogger = require "../core/helper/logger"
createSWF = require "../core/helper/swf"

commandDefaults = (yargs) ->
  yargs
  .usage('Usage: $0 [options] [command] definitionsPath')
  .options(
    "s":
      alias: "settings"
      type: "string"
      description: "Dependency settings (SWF binding, logger, etc)"
      demand: true
  )
  .demand(2)
  .strict()

argv = yargs
  .usage('Usage: $0 [options] [command] [command-specific arguments]')
  .command("domains", "Register domains", commandDefaults)
  .command("workflowTypes", "Register workflowTypes", (yargs) ->
    commandDefaults(yargs)
    .options(
      "d":
        alias: "domain"
        type: "string"
        description: "Where to register the types"
        demand: true
    )
  )
  .command("activityTypes", "Register activityTypes", (yargs) ->
    commandDefaults(yargs)
    .options(
      "d":
        alias: "domain"
        type: "string"
        description: "Where to register the types"
        demand: true
    )
  )
  .demand(1, "Please provide a command")
  .strict()
  .argv

command = argv._[0]
definitions = require path.resolve(process.cwd(), argv._[1])
settings = require path.resolve(process.cwd(), argv.settings)
domain = argv.domain

registrar = new Registrar {},
  logger: createLogger(settings.logger)
  swf: createSWF(settings.swf)

switch command
  when "domains"
    registrar.registerDomains(definitions)
  when "workflowTypes"
    registrar.registerWorkflowTypesForDomain(definitions, domain)
  when "activityTypes"
    registrar.registerActivityTypesForDomain(definitions, domain)
  else
    throw new Error("Unknown command: \"#{command}\"")

(require "death")(-> process.exit(0)) # In nomine SIGINT, SIGTERM, SIGQUIT, amen
